grammar mdsd.RPG with org.eclipse.xtext.common.Terminals

generate rPG "http://www.RPG.mdsd"

SystemRPG:
	'game' name=ID declarations+=Declaration*
;
	
Declaration:
	Locations | Relations | Moves | Entities | Teams | Attributes | Death
;
	
Locations:
	'location' loc+=Loc+
;

Loc:
	name=ID 'opponents' team=[Team]
;

Relations:
	'relations' type+=Type+
;

Type:
	'type' name=ID tExpression=TypeExpression?
;

TypeExpression:
	strong=[Type] (',' strong2+=[Type])*  '<' currentT=[Type] '<' weak=[Type] (',' weak2+=[Type])*
;


Moves:
	'moves' move+=Move+
;

Move:
	'move' name=ID eType=EType? att+=AltAttribute* effect+=Effect*
;

EType:
	'type' type=[Type]
;

Attributes:
	'attributes' attribute+=Attribute+
;

Attribute:
	'attribute' name=ID aVal=AttributeValues
;


Effect:
	'effect'  rule=Rule 
;

AttributeValues:
	'is' (lTypes=LegalType | an=AtomicNumber) | ( Rule | Require)
;

AltAttribute:
	 'attribute' attribute=[Attribute] av=AttributeValues?
;

Rule:
	'if' or=ORcondition  'then' carl=Carl
;

Carl:
	attribute=NameAttribute equal=Set change=Sum
;

Set:
	{Equal} '='
;

Entities:
	'entities' entity+=Entity+
;

Death:
	'death' req=Require
;

Entity:
	'entity' name=ID eType=EType  att+=AltAttribute* eMoves=EntityMoves
;

EntityMoves:
	'move' move+=[Move]+
;

Teams:
	'teams' Require? bs=BattleSize team+=Team+
;

Team:
	'team' name=ID members=Members
;

Members:
	'members' entity+=[Entity]+
;

BattleSize:
	'battling_member_count' AtomicNumber req=Require?
;


Require:
	'require' log=ORcondition
;

ORcondition returns Proposition:
	ANDcondition ('OR' {Or.left=current} right=ANDcondition)*
;

ANDcondition returns Proposition:
	Statement ('AND'{And.left=current} right=Statement)*
	
;

Statement returns Proposition:
	NumberComparing | '(' ORcondition ')' | type=[Type]
;

NumberComparing:
	left=Sum comp=Comparator right=Sum
;

Comparator:
	{Smaller}'<' | {SmallerEq}'<=' | {Eq} '==' | {BiggerEq}'>=' | {Bigger}'>' | {NEq}'!='
;

Sum:
	Multiply (('+' {Add.left=current} | '-' {Sub.left=current}) right=Multiply)*
;

Multiply:
	AtomicNumber (('*' {Mult.left=current} | '/' {Div.left=current}) right=AtomicNumber)*
;


AtomicNumber:
	FloatNum | IntNum | Self? NameAttribute
;

NameAttribute:
	attribute=[Attribute] 
;

SelfTargeting:
	self='self.'
;

Self:
	'self.'
;

IntNum:
	value=INT
;

FloatNum:
	i=INT '.' decimal=INT
;

LegalType:
	'Float' | 'Integer'
;
