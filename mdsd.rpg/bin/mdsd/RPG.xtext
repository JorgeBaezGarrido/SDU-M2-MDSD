grammar mdsd.RPG with org.eclipse.xtext.common.Terminals

generate rPG "http://www.RPG.mdsd"

SystemRPG:
	'game' name=ID declarations+=Declaration*;
	
Declaration:
	Locations | Relations | Moves | Entities | Teams | Attributes | Death;
	
Locations:
	'location' loc+=Loc+
;

Loc:
	name=ID 'opponents' team=[Team]
;

Relations:
	'relations' type+=Type+
;

Type:
	'type' name=ID tExpression=TypeExpression?
;

TypeExpression:
	strong=[Type] (',' strong2+=[Type])*  '<' currentT=[Type] '<' weak=[Type] (',' weak2+=[Type])*
;


Moves:
	'moves' move+=Move+
;

Move:
	'move' name=ID eType=EType? att+=AltAttribute* effect+=Effect*
;

EType:
	'type' type=[Type]
;

Attributes:
	'attributes' attribute+=Attribute+
;

Attribute:
	'attribute' name=ID aVal=AttributeValues
;


Effect:
	'effect'  Rule 
;

AttributeValues:
	'is' (lTypes=LegalType | AtomicNumber) | ( Rule | Require)
;

AltAttribute:
	 'attribute' attribute=[Attribute] av=AttributeValues?
;

Rule:
	'if' ORcondition  'then' ( SelfTargeting? attritbuteToSet+=[Attribute]'=' change+=Sum+)*
;


Entities:
	'entities' entity+=Entity+
;

Death:
	'death' con+=ORcondition+
;

Entity:
	'entity' name=ID eType=EType  att+=AltAttribute* eMoves+=EntityMoves*
;

EntityMoves:
	'move' move+=[Move]+
;

Teams:
	'teams' Require? bs=BattleSize team+=Team+
;

Team:
	'team' name=ID members+=Members+
;

Members:
	'members' entity+=[Entity]+
;

BattleSize:
	'battling_member_count' AtomicNumber req=Require?
;


Require:
	'require' ORcondition | ORcondition
;

ORcondition returns Proposition:
	ANDcondition ('OR' {Or.left=current} right=ANDcondition)*
;

ANDcondition returns Proposition:
	Statement ('AND'{And.left=current} right=Statement)*
	
;

Statement returns Proposition:
	NumberComparing | '(' ORcondition ')' | type=[Type]
;

NumberComparing returns Proposition:
	left=Sum comp=Comparator right=Sum
;

Comparator:
	'<' | '<=' | '==' | '>=' | '>' | '!='
;

Sum returns Proposition:
	Multiply (('+' {Add.left=current} | '-' {Sub.left=current}) right=Multiply)*
;

Multiply returns Proposition:
	AtomicAttribute (('*' {Mult.left=current} | '/' {Div.left=current}) right=AtomicAttribute)*
;

AtomicAttribute:
	AtomicNumber | SelfTargeting? attribute=[Attribute] 
;

AtomicNumber:
	float2=Float | int2=INT
;

SelfTargeting:
	'self.'
;

Float:
	INT '.' INT
;

LegalType:
	'Float' | 'Integer'
;
